\centering

  \begin{factorcode}
    GENERIC: value-number ( insn -- )

    M: array value-number [ value-number ] each ;

    : record-defs ( insn -- ) defs-vregs [ dup set-vn ] each ;

    M: alien-call-insn value-number record-defs ;
    M: ##callback-inputs value-number record-defs ;

    M: ##copy value-number [ src>> vreg>vn ] [ dst>> ] bi set-vn ;

    : redundant-instruction ( insn vn -- )
        swap dst>> set-vn ;

    :: useful-instruction ( insn expr -- )
        insn dst>> :> vn
        vn vn set-vn
        vn expr exprs>vns get set-at
        insn vn vns>insns get set-at ;

    : check-redundancy ( insn -- )
        dup >expr dup exprs>vns get at
        [ redundant-instruction ] [ useful-instruction ] ?if ;

    M: ##phi value-number
        dup inputs>> values [ vreg>vn ] map sift
        dup all-equal? [
            [ drop ] [ first redundant-instruction ] if-empty
        ] [ drop check-redundancy ] if ;

    M: insn value-number
        dup defs-vregs length 1 = [ check-redundancy ] [ drop ] if ;
  \end{factorcode}

\caption{Assigning value numbers in \factor|compiler.cfg.gvn|}
